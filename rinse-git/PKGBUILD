# Maintainer: Chris Nixon <chris.nixon@sigma.me.uk>
# Contributor: James An <james@jamesan.ca>

_pkgname=rinse
pkgname=$_pkgname-git
pkgver=3.0.5.r2.g42802da
pkgrel=1
pkgdesc="Rinse is a simple tool which is designed to carry out the installation of a new RPM-based distribution."
arch=('i686' 'x86_64')
url="http://collab-maint.alioth.debian.org/$_pkgname"
depends=('perl' 'perl-term-size' 'perl-libwww' 'rpm2cpio' 'rpm-org' 'wget')
#makedepends=('git' 'cpio' 'dpkg' 'rpm-org' 'wget' 'perl' 'perl-libwww' 'perl-html-parser' 'perl-uri')
makedepends=('git')
license=('GPL' 'PerlArtistic')
provides=('rinse')
conflicts=('rinse')
source=("$pkgname"::"git+https://anonscm.debian.org/cgit/collab-maint/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
    cd "$pkgname"
    (
        set -o pipefail
        git describe --long --tag | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

package() {
    make -C"$srcdir/$pkgname" DESTDIR="$pkgdir/" install

    # Correct sbin location
    mv "$pkgdir"/usr/{s,}bin
}
