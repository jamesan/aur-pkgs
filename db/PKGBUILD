# $Id: PKGBUILD 225154 2014-10-21 18:33:00Z bpiotrowski $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=6.1.23
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/database-technologies/berkeleydb/index.html"
license=('AGPL3')
depends=('gcc-libs' 'sh')
source=(http://download.oracle.com/otn/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('c6105d495ee70406d169b9f555774b73c29bd9e0')

build() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx --enable-dbm \
    --enable-stl
  make LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}/build_unix
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/docs
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
