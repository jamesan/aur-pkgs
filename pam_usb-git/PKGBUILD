# Maintainer: James An <james@jamesan.ca>
# Contributor: mickael9 <mickael9 at gmail dot com>

_pkgname=pam_usb
pkgname="$_pkgname-git"
pkgver=0.5.0.r25.g6a30dff
pkgrel=1
pkgdesc='PAM modules that enables either two-factor or password-less authentication using an USB storage device (such as an USB flash memory stick)'
arch=('i686' 'x86_64')
url="http://www.pamusb.org"
license=('GPL')
depends=(dbus-python libxml2 pam pmount pygobject udisks)
makedepends=('git')
backup=('etc/pamusb.conf')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("$_pkgname"::"git+https://github.com/aluzzardi/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    (
        set -o pipefail
        git describe --long --tag | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "$_pkgname"

    sed -i s/python/python2/ tools/*
}

build() {
    cd "$_pkgname"

    make
}

package() {
    cd "$_pkgname"

    make DESTDIR="$pkgdir" LIBDIR=usr/lib install
}
