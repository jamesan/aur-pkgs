# Maintainer: Ravindra Singh <r.coded@gmail.com>
# Contributor: James An <james@jamesan.ca>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>

pkgname=xprobe2
pkgver=0.3
pkgrel=3
pkgdesc="An active OS fingerprinting tool for TCP/IP stacks"
arch=('i686' 'x86_64')
url='http://sourceforge.net/apps/mediawiki/xprobe'
license=('GPL2')
makedepends=('gcc' 'libpcap')
source=(
    "http://downloads.sourceforge.net/project/xprobe/$pkgname/${pkgname^}%20$pkgver/$pkgname-$pkgver.tar.gz"
    ip.patch
    misc.patch
    tcp.patch
)
md5sums=(
    '3ebb89ed9380038d368327816e34ec54'
    'afe57732b82bfd100f1301bff0661d78'
    'd6a08e8d0b94c750bd5caccab81b9e0f'
    'e5a1622412523601e084ac74100edaed'
)

prepare(){
    cd "$pkgname-$pkgver"
    for _patch in ../*.patch; do
        patch -p1 < $_patch
    done
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc
    make
}

package(){
    cd "$pkgname-$pkgver"
    make DESTDIR=$pkgdir install

    # Correct non-standard paths
    install -dm755 "$pkgdir/usr/share"
    mv "$pkgdir"/usr/{man,share}
}
