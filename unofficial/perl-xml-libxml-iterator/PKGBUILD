# Maintainer: James An <james@jamesan.ca>
# Contributor: Ondrej Kucera <ondrej.kucera@centrum.cz>

pkgname=perl-xml-libxml-iterator
_realname=XML-LibXML-Iterator
pkgver=1.04
pkgrel=3
pkgdesc="XML-LibXML-Iterator perl module (XML::LibXML's tree iteration class)"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/XML-LibXML-Iterator/"
license=("Artistic")
depends=('perl>=5.10.0' 'perl-xml-libxml' 'perl-xml-nodefilter')
source=(http://search.cpan.org/CPAN/authors/id/P/PH/PHISH/${_realname}-$pkgver.tar.gz)
md5sums=('bcbf474656bd4e1a1939c2459ca91ac1')
options=(!emptydirs)

build() {
  cd ${_realname}-${pkgver}

  # Install module into the vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor || return 1
  make || return 1
}

check() {
  cd ${_realname}-${pkgver}

  make test || return 1
}

package() {
  cd ${_realname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # Remove .packlist and perllocal.pod files.
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name 'perllocal.pod' -delete
}
