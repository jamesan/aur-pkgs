# Maintainer: twa022 <twa022@gmail.com>
# Contributor: James An <james@jamesan.ca>

pkgname=dockbarx-dock-theme
pkgver=1
pkgrel=3
pkgdesc="DockBarX Theme"
arch=('any')
url='http://bigrza.deviantart.com/art/Dock-theme-for-DockbarX-164283521'
license=('GPL')
depends=('dockbarx')
makedepends=('imagemagick')
groups=('dockbarx-themes')
source=('http://www.deviantart.com/download/164283521/Dock_theme_for_DockbarX_by_BigRZA.zip')
md5sums=('736858c069657576394c4697cb3fd47b')

DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -H -U Mozilla -O %o %u')

prepare() {
  for _dock in Dock DockV; do
    mkdir -p ${_dock}-Tight
    tar -zxf ${_dock}.tar.gz -C ${_dock}-Tight
  done
}

build() {
  sed -i 's:aspect_ratio="3/2":aspect_ratio="1/1":' Dock-Tight/config
  sed -i 's:aspect_ratio="4/3":aspect_ratio="1/1":' DockV-Tight/config

  for _dock in Dock DockV; do
    sed -i "s:<theme name = \"${_dock}\">:<theme name = \"${_dock}-Tight\">:" ${_dock}-Tight/config
    for _png in $(find ${_dock}-Tight/pixmaps -name *png); do
      if [ "$(basename ${_png})" = 'launcherbutton.png' ]; then
        convert -shave 11x0 ${_png} ${_png}
      else
        convert -shave 10x0 ${_png} ${_png}
      fi
    done
    tar -czf ${_dock}-Tight.tar.gz -C ${_dock}-Tight pixmaps config
  done
}

package() {
  for _file in *.tar.gz; do
    install -Dm644 ${_file} ${pkgdir}/usr/share/dockbarx/themes/${_file}
  done
}
