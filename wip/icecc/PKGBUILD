# $Id$
# Maintainer: Laszlo Papp <lpapp@kde.org>

pkgname=icecc
_pkgname=icecream
pkgver=1.1rc1
pkgrel=1
pkgdesc="Distributed compiler with a central scheduler to share build load"
arch=('i686' 'x86_64')
url="https://github.com/$pkgname/$_pkgname"
license=('GPLv2')
depends=(
'docbook2x'
'perl-xml-sax-expat'
)
backup=("etc/${pkgname}.conf")
install="${pkgname}.install"
source=(
    "https://github.com/$pkgname/$_pkgname/archive/$pkgver.tar.gz"
    "${pkgname}.service"
    "${pkgname}-scheduler.service"
    "${pkgname}.conf"
)
md5sums=(
    'fd2488ecbb62ea2abbd73d263ae674e5'
    '3383f065c1b42f5fa62546d0ac54dc6d'
    '37e4509d46e787d7b2963d9337ee189a'
    '5fe4d4ff6da71787e146a8a69b419f45'
)

prepare() {
    sed -i s/docbook-to-man/docbook2man/ "${_pkgname}-${pkgver}/configure.ac"
}

build() {
    cd "${_pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --prefix=/usr --enable-shared
    make all
}

package() {
    cd "${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -D -m755 "../${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
    install -D -m644 "../${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
    install -D -m644 "../${pkgname}-scheduler.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}-scheduler.service"

    mv "${pkgdir}/usr/sbin/icecc-scheduler" "${pkgdir}/usr/bin/"
    mv "${pkgdir}/usr/sbin/iceccd" "${pkgdir}/usr/bin/"
    rmdir "${pkgdir}/usr/sbin"

    # FIXME: Remove this later. It is necessary for the monitor as of now
    install -D -m755 services/logging.h "${pkgdir}/usr/include/${pkgname}/logging.h"
}

