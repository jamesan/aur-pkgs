pkgname=xf86-input-mtrack-git
pkgrel=1
pkgver=0.2.0.49.g7977197
pkgdesc="A multitouch X driver using the kernel MT protocol"
epoch=1
arch=('i686' 'x86_64')
url="http://github.com/BlueDragonX/xf86-input-mtrack"
license=('GPL')
depends=('mtdev' 'libxss')
makedepends=('git' 'xorg-server-devel' 'resourceproto' 'glproto')
provides=('xf86-input-mtrack')
conflicts=('xf86-input-mtrack')
backup=('etc/X11/xorg.conf.d/10-mtrack.conf')
source=(10-mtrack.conf
        git+https://github.com/BlueDragonX/xf86-input-mtrack.git)
md5sums=(ffb540330d92957e0da28af5a005136e
         SKIP)

pkgver() {
  cd "$srcdir"/xf86-input-mtrack
  local ver="$(git describe)"
  ver="${ver#v}"
  echo "${ver//-/.}"
}

build() {
  cd "$srcdir"/xf86-input-mtrack

  mkdir m4
  autoreconf --install
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/xf86-input-mtrack

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/10-mtrack.conf" "$pkgdir/etc/X11/xorg.conf.d/10-mtrack.conf"
}
