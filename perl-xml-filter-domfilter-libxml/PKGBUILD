# Maintainer: James An <james@jamesan.ca>
# Contributor: Ondrej Kucera <ondrej.kucera@centrum.cz>

pkgname=perl-xml-filter-domfilter-libxml
_realname=XML-Filter-DOMFilter-LibXML
pkgver=0.02
pkgrel=6
pkgdesc="XML-Filter-DOMFilter-LibXML perl module (SAX filter allowing DOM processing of selected subtrees)"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/XML-Filter-DOMFilter-LibXML/"
license=("unknown")
depends=('perl' 'perl-xml-libxml>=1.53')
source=(http://search.cpan.org/CPAN/authors/id/P/PA/PAJAS/${_realname}-$pkgver.tar.gz)
md5sums=('9089d02fbd5ff914070af4c3b9142f02')
options=(!emptydirs)

build() {
  cd ${_realname}-${pkgver}

  # Install module into the vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor || return 1
  make || return 1
}

check() {
  cd ${_realname}-${pkgver}

  make test || return 1
}

package() {
  cd ${_realname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # Remove .packlist and perllocal.pod files.
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name 'perllocal.pod' -delete
}
