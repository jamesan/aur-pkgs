# Maintainer: Gustavo alvarez <sl1pkn07@gmail.com>
# Contributor: Ner0 (based on chromium-pepperflash-dev PKGBUILD)

pkgname=chromium-widevine-dev
pkgver=1.4.8.823
pkgrel=1
pkgdesc="The Widevine Media Optimizer is a browser plugin designed for the viewing of premium video content (only for chromium-dev)"
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('gcc-libs')
options=('!strip')

[ "${CARCH}" = "i686" ] && _rpm_arch="i386"
[ "${CARCH}" = "x86_64" ] && _rpm_arch="x86_64"

source=('https://www.google.com/chrome/intl/en/eula_text.html')
sha1sums=('1ffba5152cb749300a016efec909b828eba9a64a')
_metadata="$(curl -sL "https://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/repodata/other.xml.gz" | gzip -df)"
_rpm_ver="$(echo "${_metadata}" | grep -e unstable | awk -v FS='ver="' -v RS='" ' '$0=$2'  | head -n 1)"
_rpm_rel="$(echo "${_metadata}" | grep -e unstable | awk -v FS='rel="' -v RS='"/' '$0=$2' | head -n 1)"
_rpm_sha1sum="$(echo "${_metadata}" | grep -e unstable | awk -v FS='pkgid="' -v RS='" ' '$0=$2' | head -n 1)"
source+=("http://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/google-chrome-unstable-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm")
sha1sums+=("${_rpm_sha1sum}")
noextract=("google-chrome-unstable-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm")

pkgver() {
  bsdtar -xf "google-chrome-unstable-${_rpm_ver}-${_rpm_rel}.${_rpm_arch}.rpm" opt/google/chrome-unstable/{chrome,libwidevinecdm.so}
  echo "$(strings opt/google/chrome*/chrome | grep -C 1 " (version:" | tail -1)"
}

prepare() {
  # Extract EULA text between these opening and closing div tags.
  sed -n '/<div class="compact">/','/<\/div>/p' eula_text.html | \
    # Remove in-line strong tags and remove other lines with tags.
    sed 's/<\/\?strong>//g;/>/d;s/^\s*//' | \
    # Insert empty line ahead of each numbered list item.
    sed -r 's/^([0-9]|\([a-z]\)|[a-z]\.)/\n\1/' | \
    # Insert empty line after EULA headings.
    sed -r 's/(Google Chrome)( Additional|)( Terms of Service)/\1\2\3\n/' |
    # Insert empty line before and after date line between the two TOS.
    sed -r 's/(August 12, 2010)/\n\1\n/' >| eula_text.txt
}

package() {
  install -Dm755 opt/google/chrome*/libwidevinecdm.so "${pkgdir}/usr/lib/chromium-dev/libwidevinecdm.so"
  install -Dm644 eula_text.txt "${pkgdir}/usr/share/licenses/${pkgname}/eula_text.txt"
}
